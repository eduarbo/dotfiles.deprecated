#!/usr/bin/env zsh
source ${0:A:h}/../../deploy
source .zshenv

install() {
  local zshenv=/etc/zshenv
  case $(_os) in
    macos)
      brew install zsh zsh-history-substring-search fd
      ;;
    fedora)
      # TODO: Remove when F28 comes out and install fd-find instead
      sudo dnf copr enable -y keefle/fd
      sudo dnf install -y zsh fd
      ;;
    arch)
      sudo pacman --needed --noconfirm -S zsh fd
      zshenv=/etc/zsh/zshenv
      ;;
    debian)
      sudo apt-get update && sudo apt-get install -y zsh fd
      ;;
  esac

  [[ $SHELL == */zsh ]] || chsh -s $(which zsh) $(whoami)

  local zsh="$(which zsh)"
  grep $zsh /etc/shells &>/dev/null || sudo tee -a /etc/shells <<<$zsh
  sudo tee $zshenv <<<"export ZDOTDIR=\"${ZDOTDIR/$HOME/\$HOME}\""
}

update() {
  if [[ -e ${ZPLGM[BIN_DIR]}/zplugin.zsh ]]; then
    source ${ZPLGM[BIN_DIR]}/zplugin.zsh
    zplugin self-update
    zplugin update --all
    zplugin compile --all
  fi
}

link() {
  mklink .zsh* $ZDOTDIR/
  mklink functions $ZDOTDIR/
  [[ $(_os) == macos ]] && mklink .zshenv ~/.zshenv
}

clean() {
  rm -rfv $ZDOTDIR
  [[ $(_os) == macos ]] && rm -f ~/.zshenv
}

init "$@"
