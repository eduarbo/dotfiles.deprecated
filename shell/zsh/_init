#!/usr/bin/env zsh
source ${0:A:h}/../../deploy
source .zshenv

# Dependencies

common_packages=(
  zsh
  fd
  fasd
)

brew_packages=(
  zsh-history-substring-search
)

dnf_packages=(
  make
  emacs
)

install() {
  case $(_os) in
    macos)
      brew install ${common_packages[@]} ${brew_packages[@]}
      ;;
    fedora)
      # TODO: Remove when F28 comes out and install fd-find instead
      sudo dnf copr enable -y keefle/fd
      sudo dnf install -y ${common_packages[@]} ${dnf_packages[@]}
      ;;
  esac

  # Do not complaint about insecure directories
  sudo chmod g-w /usr/local/share/zsh/site-functions
  sudo chmod g-w /usr/local/share/zsh

  # Make ZSH the default shell
  local zsh_dir="$(which zsh)"
  local zshenv_dir=/etc/zshenv
  grep $zsh_dir /etc/shells &>/dev/null || sudo tee -a /etc/shells <<<$zsh_dir
  [[ $SHELL == */zsh ]] || sudo chsh -s $zsh_dir
  sudo tee $zshenv_dir <<<"export ZDOTDIR=\"${ZDOTDIR/$HOME/\$HOME}\""
}

update() {
  # Update zpugins and recompile
  if [[ -e ${ZPLGM[BIN_DIR]}/zplugin.zsh ]]; then
    source ${ZPLGM[BIN_DIR]}/zplugin.zsh
    zplugin self-update
    zplugin update --all
    zplugin compile --all
  fi
}

link() {
  mklink .zsh* $ZDOTDIR/
  mklink functions $ZDOTDIR/

  case $(_os) in
    macos)
      mklink .zshenv ~/.zshenv
      ;;
  esac
}

clean() {
  rm -rfv "$ZDOTDIR" "$ZSH_CACHE/*";

  case $(_os) in
    macos)
      rm -f "~/.zshenv"
      ;;
  esac
}

init "$@"
