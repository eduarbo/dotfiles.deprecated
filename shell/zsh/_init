#!/usr/bin/env zsh
source ${0:A:h}/../../deploy
source .zshenv

install() {
  local zshenv=/etc/zsh/zshenv
  case $(_os) in
    macos)
      brew install zsh zsh-history-substring-search fasd fd
      zshenv=/etc/zshenv
      ;;
    fedora)
      # TODO: Remove when F28 comes out and install fd-find instead
      sudo dnf copr enable keefle/fd
      sudo dnf install -y zsh fasd fd
      ;;
    arch)
      sudo pacman --needed --noconfirm -S zsh fasd fd
      ;;
    debian)
      sudo add-apt-repository ppa:aacebedo/fasd
      sudo apt-get update && sudo apt-get install -y zsh fasd fd
      ;;
  esac

  [[ $SHELL == */zsh ]] || chsh -s $(which zsh) $(whoami)

  local zsh="$(which zsh)"
  grep $zsh /etc/shells &>/dev/null || sudo tee -a /etc/shells <<<$zsh
  sudo tee $zshenv <<<"export ZDOTDIR=\"${ZDOTDIR/$HOME/\$HOME}\""
  exec $SHELL -l
}

update() {
  if [[ -e ${ZPLGM[BIN_DIR]}/zplugin.zsh ]]; then
    source ${ZPLGM[BIN_DIR]}/zplugin.zsh
    zplugin self-update
    zplugin update --all
    zplugin compile --all
  fi
}

link() {
  mklink .zsh* $ZDOTDIR/
  mklink functions $ZDOTDIR/
  [[ $(_os) == macos ]] && mklink .zshenv ~/.zshenv
}

clean() {
  rm -rfv $ZDOTDIR
  [[ $(_os) == macos ]] && rm -f ~/.zshenv
}

init "$@"
