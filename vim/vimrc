" .vimrc
" Author: Eduardo Ruiz <eduarbo@gmail.com>
" Source: https://github.com/eduarbo/dotfiles/tree/master/vim
"
" Preamble ---------------------------------------------------------------- {{{
filetype off

if has('vim_starting')
    set nocompatible               " Be iMproved

    " Required:
    set runtimepath+=~/.vim/bundle/neobundle.vim/
endif

" Required:
call neobundle#begin(expand('~/.vim/bundle/'))

" Let NeoBundle manage NeoBundle
" Required:
NeoBundleFetch 'Shougo/neobundle.vim'
" }}}
" Plugins ----------------------------------------------------------------- {{{
NeoBundle 'bling/vim-airline'
NeoBundle 'Raimondi/delimitMate'
NeoBundle 'tpope/vim-commentary'
NeoBundle 'tpope/vim-endwise'
NeoBundle 'scrooloose/syntastic'
NeoBundle 'tpope/vim-repeat'
NeoBundle 'tpope/vim-surround'
NeoBundle 'scrooloose/nerdtree'
NeoBundle 'terryma/vim-multiple-cursors'
NeoBundle 'kshenoy/vim-signature'
NeoBundle 'kien/ctrlp.vim'
NeoBundle 'SirVer/ultisnips'
NeoBundle 'tpope/vim-markdown'
NeoBundle 'godlygeek/tabular'
NeoBundle 'Valloric/YouCompleteMe'
NeoBundle 'vim-scripts/YankRing.vim'
NeoBundle 'rking/ag.vim'
NeoBundle 'vim-scripts/SyntaxRange'
NeoBundle 'tybenz/vimdeck'
NeoBundle 'slim-template/vim-slim'
NeoBundle 'majutsushi/tagbar'

" browse the vim undo tree
NeoBundleLazy 'sjl/gundo.vim', { 'autoload' : {'commands': 'GundoToggle'}}

" SQL {{{
NeoBundleLazy 'exu/pgsql.vim', {'autoload':
            \ {'filetypes': ['pgsql']}}
" }}}
" HTML {{{
NeoBundleLazy 'othree/html5.vim', {'autoload':
            \ {'filetypes': ['html', 'xhttml', 'css']}}
NeoBundleLazy 'mattn/emmet-vim', {'autoload':
            \ {'filetypes': ['html', 'xhttml', 'handlebars', 'css', 'xml', 'xls', 'markdown']}}
" }}}
" Colorschemes {{{
NeoBundle 'tomasr/molokai'
NeoBundle 'sjl/badwolf'
" }}}
" Git {{{
NeoBundle 'tpope/vim-fugitive'
" Git viewer
NeoBundleLazy 'gregsexton/gitv', {'depends':['tpope/vim-fugitive'],
            \ 'autoload': {'commands':'Gitv'}}
" }}}
" Javascript {{{
NeoBundleLazy 'pangloss/vim-javascript', {'autoload':
            \ {'filetypes': ['javascript', 'haml']}}
NeoBundleLazy 'jelera/vim-javascript-syntax', {'autoload':
            \ {'filetypes': ['javascript', 'haml']}}
NeoBundleLazy 'nono/vim-handlebars', {'autoload':
            \ {'filetypes': ['handlebars']}}
" }}}
" Ruby {{{
NeoBundleLazy 'vim-ruby/vim-ruby', {'autoload':
            \ {'filetypes': ['ruby', 'rails']}}
NeoBundleLazy 'tpope/vim-rails.git', {'autoload':
            \ {'filetypes': ['ruby', 'rails']}}
NeoBundleLazy 'tpope/vim-cucumber', {'autoload':
            \ {'filetypes': ['ruby', 'rails']}}
NeoBundleLazy 'skwp/vim-rspec', {'autoload':
            \ {'filetypes': ['ruby', 'rails']}}
" }}}

call neobundle#end()
" }}}
" Basic options ----------------------------------------------------------- {{{
filetype plugin indent on

" If there are uninstalled bundles found on startup,
" this will conveniently prompt you to install them.
NeoBundleCheck

inoremap jj <Esc> " Professor VIM says '87% of users prefer jj over esc', jj abrams disagrees

let mapleader = ","
let maplocalleader = "\\"

scriptencoding utf-8

set encoding=utf-8
set modeline
set autoindent                  " Automatically set the indent of a new line (local to buffer)
set showmode                    " Show current mode down the bottom
set showcmd                     " Show incomplete cmds down the bottom
set hidden
set visualbell                  " No sounds
set ttyfast                     " Improves redrawing
set ruler                       " Always show current positions along the bottom
set backspace=indent,eol,start  " Allow backspace in insert mode
set number                      " Line numbers are good
set laststatus=2                " Always show the status line
set history=1000                " Store lots of :cmdline history
set list                        " Hide invisible chars
set listchars=tab:▸\ ,eol:↵,extends:↷,precedes:↶,trail:·
set shell=/bin/zsh\ --login
set lazyredraw                  " Do not redraw while running macros
set matchtime=3                 " how many tenths of a second to blink
set showbreak=↪
set splitbelow
set splitright
set fillchars=diff:⣿,vert:┊     " Customize text for closed folds
set autowrite
set autoread                    " Reload files changed outside vim
set shiftround                  " When at 3 spaces, and I hit > ... go to 4, not 5
set title                       " set the terminal title to the current file
set t_vb=                       " Visual bell disabled on Linux
set linebreak
set dictionary=/usr/share/dict/words
set spellfile=~/.vim/custom-dictionary.utf-8.add
set pastetoggle=<F2>            " Use it for pasting large amounts of text into Vim, disabling all kinds of smartness and just pasting a whole buffer of text
set lazyredraw

if v:version > 702
    set norelativenumber            " It is much faster
    set undofile
    set undoreload=10000
    set colorcolumn=+1
endif

" Don't try to highlight lines longer than 800 characters.
set synmaxcol=800

" Time out on key codes but not mappings.
" Basically this makes terminal Vim work sanely.
set notimeout
set ttimeout
set ttimeoutlen=10

" Make Vim able to edit crontab files again.
set backupskip=/tmp/*,/private/tmp/*"

" Better Completion
set complete=.,w,b,u,t
set completeopt=longest,menuone,preview

" Resize splits when the window is resized
au VimResized * :wincmd =

" Cursorline {{{
" Only show cursorline in the current window and in normal mode.

augroup cline
    au!
    au WinLeave,InsertEnter * set nocursorline
    au WinEnter,InsertLeave * set cursorline
augroup END
" }}}
" Wildmenu completion {{{
set wildmenu
set wildmode=list:longest

set wildignore+=.hg,.git,.svn                    " Version control
set wildignore+=*.aux,*.out,*.toc                " LaTeX intermediate files
set wildignore+=*.jpg,*.bmp,*.gif,*.png,*.jpeg   " binary images
set wildignore+=*.o,*.obj,*.exe,*.dll,*.manifest " compiled object files
set wildignore+=*.bak,*.?~,*.??~,*.???~,*.~      " Backup files
set wildignore+=*.spl                            " compiled spelling word lists
set wildignore+=*.sw?                            " Vim swap files
set wildignore+=*.DS_Store                       " OSX bullshit
set wildignore+=*.jar                            " Java archives
set wildignore+=*.pyc                            " Python bite code
set wildignore+=*.luac                           " Lua byte code
set wildignore+=*.orig                           " Merge resolution files
" }}}
" Line Return {{{
" Make sure Vim returns to the same line when you reopen a file.
" Thanks, Amit
augroup line_return
    au!
    au BufReadPost *
                \ if line("'\"") > 0 && line("'\"") <= line("$") |
                \     execute 'normal! g`"zvzz' |
                \ endif
augroup END
" }}}
" Tabs, spaces, wrapping {{{
set tabstop=2
set shiftwidth=2
set softtabstop=2
set expandtab
set wrap
set textwidth=80
set formatoptions=qrn1
" }}}
" Backups {{{
set backup                        " enable backups
set noswapfile                    " It's 2012, Vim.

set undodir=$HOME/.vim/tmp/undo/     " undo files
set backupdir=$HOME/.vim/tmp/backup/ " backups
set directory=$HOME/.vim/tmp/swap/   " swap files

" Make those folders automatically if they don't already exist.
if !isdirectory(expand(&undodir))
    call mkdir(expand(&undodir), "p")
endif
if !isdirectory(expand(&backupdir))
    call mkdir(expand(&backupdir), "p")
endif
if !isdirectory(expand(&directory))
    call mkdir(expand(&directory), "p")
endif
" }}}
" Color scheme {{{
syntax enable
set background=dark
let g:badwolf_html_link_underline = 0
let g:badwolf_tabline = 2
" Turn on CSS properties highlighting
" let g:badwolf_css_props_highlight = 1
" Make the gutters darker than the background.
" let g:badwolf_darkgutter = 1
colorscheme badwolf
" }}}
" }}}
" Abbreviations ----------------------------------------------------------- {{{
function! EatChar(pat)
    let c = nr2char(getchar(0))
    return (c =~ a:pat) ? '' : c
endfunction

function! MakeSpacelessIabbrev(from, to)
    execute "iabbrev <silent> ".a:from." ".a:to."<C-R>=EatChar('\\s')<CR>"
endfunction
function! MakeSpacelessBufferIabbrev(from, to)
    execute "iabbrev <silent> <buffer> ".a:from." ".a:to."<C-R>=EatChar('\\s')<CR>"
endfunction

call MakeSpacelessIabbrev('gh/',  'http://github.com/')

iabbrev ldis ಠ_ಠ
iabbrev lsad ಥ_ಥ
iabbrev lhap ಥ‿ಥ
iabbrev lmis ಠ‿ಠ

" }}}
" Convenience mappings ---------------------------------------------------- {{{
" Fuck you, help key.
noremap  <F1> <ESC>
inoremap <F1> <ESC>

" Format File
map <F6> mzgg=G'z

" Stop it, hash key.
inoremap # X<BS>#

" Kill window
nnoremap K :q<cr>

" Sort lines
nnoremap <leader>s vip:!sort<cr>
vnoremap <leader>s :!sort<cr>

" Tabs
nnoremap <C-(> :tabprev<cr>
nnoremap <C-)> :tabnext<cr>
nnoremap <C-t> :tabnew<cr>

"key mappings for identation
" nnoremap < <<_
" nnoremap > >>_
vnoremap < <gv
vnoremap > >gv

inoremap <C-v> <C-o>"+P

nnoremap <C-q> :q<cr>

nnoremap ; :

" Use c-\ to do c-] but open it in a new split.
nnoremap <c-\> <c-w>v<c-]>zvzz

" Move lines
nnoremap <A-j> :m .+1<CR>==
nnoremap <A-k> :m .-2<CR>==
inoremap <A-j> <Esc>:m .+1<CR>==gi
inoremap <A-k> <Esc>:m .-2<CR>==gi
vnoremap <A-j> :m '>+1<CR>gv=gv
vnoremap <A-k> :m '<-2<CR>gv=gv

" Insert New Line
nnoremap <S-Enter> O<ESC> " awesome, inserts new line without going into insert mode
nnoremap <Enter> o<ESC>

" Go to previous file
nnoremap <leader>/ <C-^>

" Sigh.
nnoremap <leader><cr> :silent !/usr/local/bin/ctags -R . && sed -i .bak -E -e '/^[^	]+	[^	]+.py	.+v$/d' tags<cr>:redraw!<cr>

" Clean trailing whitespace
nnoremap <leader>w mz:%s/\s\+$//<cr>:let @/=''<cr>`z

" Copy/Paste to and from Desktop Environment
noremap <leader>y "+y
noremap <leader>p "+p
noremap <leader>P "+P

" Make the Y behavior similar to D & C
nnoremap Y y$

" Insert the directory of the current buffer in command line mode
cnoremap <expr> %% getcmdtype() == ':' ? expand('%:h').'/' : '%%'

" Select entire buffer
nnoremap vaa ggvGg_
nnoremap Vaa ggVG

" Change case
inoremap <C-u> <esc>mzgUiw`za

" Panic Button
nnoremap <leader>` mzggg?G`z

" Emacs bindings in command line mode
cnoremap <c-a> <home>
cnoremap <c-e> <end>

" Diffoff
nnoremap <leader>D :diffoff!<cr>

" Formatting, TextMate-style
nnoremap Q gqip
vnoremap Q gq

" Reformat line.
" I never use l as a macro register anyway.
nnoremap ql gqq

" Easier linewise reselection
nnoremap <leader>V V`]

" Indent/dedent/autoindent what you just pasted.
nnoremap <lt>> V`]<
nnoremap ><lt> V`]>
nnoremap =- V`]=

" Keep the cursor in place while joining lines
nnoremap J mzJ`z

" Split line (sister to [J]oin lines)
" The normal use of S is covered by cc, so don't worry about shadowing it.
nnoremap S i<cr><esc>^mwgk:silent! s/\v +$//<cr>:noh<cr>`w

" Source
vnoremap <leader>S y:execute @@<cr>:echo 'Sourced selection.'<cr>
nnoremap <leader>S ^vg_y:execute @@<cr>:echo 'Sourced line.'<cr>

" HTML tag closing
inoremap <C-_> <space><bs><esc>:call InsertCloseTag()<cr>a

" Select (charwise) the contents of the current line, excluding indentation.
" Great for pasting Python lines into REPLs.
nnoremap vv ^vg_

" Sudo to write
cnoremap w!! w !sudo tee % >/dev/null

" Typos
command! -bang E e<bang>
command! -bang Q q<bang>
command! -bang W w<bang>
command! -bang QA qa<bang>
command! -bang Qa qa<bang>
command! -bang Wa wa<bang>
command! -bang WA wa<bang>
command! -bang Wq wq<bang>
command! -bang WQ wq<bang>

" I suck at typing.
nnoremap <localleader>- ==
vnoremap - =

" Toggle [i]nvisible characters
nnoremap <leader>i :set list!<cr>

" Unfuck my screen
nnoremap U :syntax sync fromstart<cr>:redraw!<cr>
" }}}
" Quick editing ----------------------------------------------------------- {{{
nnoremap <leader>ev :vsplit $MYVIMRC<cr>
nnoremap <leader>ej :vsplit ~/.vim/jshintrc<cr>
nnoremap <leader>ef :vsplit ~/.config/fish/config.fish<cr>
nnoremap <leader>ez :vsplit ~/.dotfiles/zsh/zshrc<cr>

nnoremap <leader>ed :vsplit ~/.vim/custom-dictionary.utf-8.add<cr>
nnoremap <leader>eb :vsplit ~/.bash_profile<cr>
nnoremap <leader>eg :vsplit ~/.gitconfig<cr>
" }}}
" Searching and movement -------------------------------------------------- {{{
" Use sane regexes.
nnoremap / /\v
vnoremap / /\v

set ignorecase
set smartcase
set incsearch
set showmatch
set hlsearch
set gdefault

set scrolloff=3
set sidescroll=1
set sidescrolloff=10

set virtualedit+=block

noremap <silent> <leader><space> :noh<cr>:call clearmatches()<cr>

runtime macros/matchit.vim
map <tab> %
silent! unmap [%
silent! unmap ]%

" Made D behave
nnoremap D d$

" Don't move on *
nnoremap * *<c-o>

" Keep search matches in the middle of the window.
nnoremap n nzzzv
nnoremap N Nzzzv

" Same when jumping around
nnoremap g; g;zz
nnoremap g, g,zz
nnoremap <c-o> <c-o>zz

" Easier to type, and I never use the default behavior.
noremap H ^
noremap L $
vnoremap L g_

" Heresy
inoremap <c-a> <esc>I
inoremap <c-e> <esc>A

" gi already moves to "last place you exited insert mode", so we'll map gI to
" something similar: move to last change
nnoremap gI `.

" Fix linewise visual selection of various text objects
nnoremap VV V
nnoremap Vit vitVkoj
nnoremap Vat vatV
nnoremap Vab vabV
nnoremap VaB vaBV

" Toggle "keep current line in the center of the screen" mode
nnoremap <leader>C :let &scrolloff=999-&scrolloff<cr>

" Directional Keys {{{
" It's 2012.
noremap j gj
noremap k gk
noremap gj j
noremap gk k

" Easy buffer navigation
noremap <C-h> <C-w>h
noremap <C-j> <C-w>j
noremap <C-k> <C-w>k
noremap <C-l> <C-w>l

noremap <leader>v <C-w>v
noremap <leader>h <C-w>s
" }}}
" Visual Mode */# from Scrooloose {{{
function! s:VSetSearch()
    let temp = @@
    norm! gvy
    let @/ = '\V' . substitute(escape(@@, '\'), '\n', '\\n', 'g')
    let @@ = temp
endfunction

vnoremap * :<C-u>call <SID>VSetSearch()<CR>//<CR><c-o>
vnoremap # :<C-u>call <SID>VSetSearch()<CR>??<CR><c-o>
" }}}
" List navigation {{{
nnoremap <left>  :cprev<cr>zvzz
nnoremap <right> :cnext<cr>zvzz
nnoremap <up>    :lprev<cr>zvzz
nnoremap <down>  :lnext<cr>zvzz
" }}}
" }}}
" Folding ----------------------------------------------------------------- {{{
set foldlevelstart=0
set foldmethod=marker

" Space to toggle folds.
nnoremap <Space> za
vnoremap <Space> za

" "Refocus" folds
nnoremap <leader>z zMzvzz

" Make zO recursively open whatever top level fold we're in, no matter where the
" cursor happens to be.
nnoremap zO zCzO

" "Focus" the current line.  Basically:
"
" 1. Close all folds.
" 2. Open just the folds containing the current line.
" 3. Move the line to a little bit (15 lines) above the center of the screen.
" 4. Pulse the cursor line.  My eyes are bad.
"
" This mapping wipes out the z mark, which I never use.
"
" I use :sus for the rare times I want to actually background Vim.
nnoremap <c-z> mzzMzvzz15<c-e>`z:Pulse<cr>

function! MyFoldText() " {{{
    let line = getline(v:foldstart)

    let nucolwidth = &fdc + &number * &numberwidth
    let windowwidth = winwidth(0) - nucolwidth - 3
    let foldedlinecount = v:foldend - v:foldstart

    " expand tabs into spaces
    let onetab = strpart('          ', 0, &tabstop)
    let line = substitute(line, '\t', onetab, 'g')

    let line = strpart(line, 0, windowwidth - 2 -len(foldedlinecount))
    let fillcharcount = windowwidth - len(line) - len(foldedlinecount)
    return line . '…' . repeat(" ",fillcharcount) . foldedlinecount . '…' . ' '
endfunction " }}}
set foldtext=MyFoldText()
" }}}
" Filetype-specific ------------------------------------------------------- {{{
" C {{{
augroup ft_c
    au!
    au FileType c setlocal foldmethod=marker foldmarker={,}
augroup END
" }}}
" Clojure {{{
let g:clojure_fold_extra = [
            \ 'defgauge',
            \ 'defmeter',
            \ 'defhistogram',
            \ 'defcounter',
            \ 'deftimer',
            \
            \ 'defdb',
            \ 'defentity',
            \ 'defaspect',
            \ 'add-aspect',
            \ 'defmigration',
            \
            \ 'defsynth',
            \ 'definst',
            \ 'defproject',
            \
            \ 'defroutes',
            \
            \ 'defrec',
            \
            \ 'defparser',
            \
            \ 'defform',
            \ 'defform-',
            \
            \ 'defpage',
            \ 'defsketch'
            \
            \ ]

let g:clojure_highlight_extra_defs = [
            \ 'defproject',
            \
            \ 'defroutes'
            \ ]

augroup ft_clojure
    au!

    au BufNewFile,BufRead riemann.config set filetype=clojure
    au FileType clojure silent! call TurnOnClojureFolding()
    au FileType clojure compiler clojure
    au FileType clojure setlocal report=100000

    au FileType clojure RainbowParenthesesActivate
    au syntax clojure RainbowParenthesesLoadRound
    au syntax clojure RainbowParenthesesLoadSquare
    au syntax clojure RainbowParenthesesLoadBraces

    " Friendlier Paredit mappings.
    au FileType clojure noremap <buffer> () :<c-u>call PareditWrap("(", ")")<cr>
    au FileType clojure noremap <buffer> )( :<c-u>call PareditSplice()<cr>
    au FileType clojure noremap <buffer> (( :<c-u>call PareditMoveLeft()<cr>
    au FileType clojure noremap <buffer> )) :<c-u>call PareditMoveRight()<cr>
    au FileType clojure noremap <buffer> (j :<c-u>call PareditJoin()<cr>
    au FileType clojure noremap <buffer> (s :<c-u>call PareditSplit()<cr>
    au FileType clojure noremap <buffer> [ :<c-u>call PareditSmartJumpOpening(0)<cr>
    au FileType clojure noremap <buffer> ] :<c-u>call PareditSmartJumpClosing(0)<cr>
    " )))

    " Indent top-level form.
    au FileType clojure nmap <buffer> <localleader>= mz99[(v%='z
    " ])
augroup END
" }}}
" Clojurescript {{{
augroup ft_clojurescript
    au!

    au BufNewFile,BufRead *.cljs set filetype=clojurescript
    au FileType clojurescript call TurnOnClojureFolding()

    " Indent top-level form.
    au FileType clojurescript nmap <buffer> <localleader>= v((((((((((((=%
augroup END
" }}}
" CSS and Sass{{{
augroup ft_css
    au!

    au BufNewFile,BufRead *.less setlocal filetype=less

    au Filetype sass,scss,less,css setlocal foldmethod=marker
    au Filetype sass,scss,less,css setlocal foldmarker={,}
    au Filetype sass,scss,less,css setlocal omnifunc=csscomplete#CompleteCSS
    au Filetype sass,scss,less,css setlocal iskeyword+=-

    au BufNewFile,BufRead *.less,*.css,*.scss nnoremap <buffer> <localleader>S ?{<CR>jV/\v^\s*\}?$<CR>k:sort<CR>:noh<CR>

    " Make {<cr> insert a pair of brackets in such a way that the cursor is correctly
    " positioned inside of them AND the following code doesn't get unfolded.
    au BufNewFile,BufRead *.less,*.css,*.sass,*.scss inoremap <buffer> {<cr> {}<left><cr><space><space><space><space>.<cr><esc>kA<bs>
    " }

augroup END
" }}}
" Django {{{
augroup ft_django
    au!

    au BufNewFile,BufRead urls.py           setlocal nowrap
    au BufNewFile,BufRead urls.py           normal! zR
    au BufNewFile,BufRead dashboard.py      normal! zR
    au BufNewFile,BufRead local_settings.py normal! zR

    au BufNewFile,BufRead admin.py     setlocal filetype=python.django
    au BufNewFile,BufRead urls.py      setlocal filetype=python.django
    au BufNewFile,BufRead models.py    setlocal filetype=python.django
    au BufNewFile,BufRead views.py     setlocal filetype=python.django
    au BufNewFile,BufRead settings.py  setlocal filetype=python.django
    au BufNewFile,BufRead settings.py  setlocal foldmethod=marker
    au BufNewFile,BufRead forms.py     setlocal filetype=python.django
    au BufNewFile,BufRead common_settings.py  setlocal filetype=python.django
    au BufNewFile,BufRead common_settings.py  setlocal foldmethod=marker
augroup END
" }}}
" Firefox {{{
augroup ft_firefox
    au!
    au BufRead,BufNewFile ~/Library/Caches/*.html setlocal buftype=nofile
augroup END
" }}}
" Fish {{{
augroup ft_fish
    au!

    au BufNewFile,BufRead *.fish setlocal filetype=fish

    au FileType fish setlocal foldmethod=marker foldmarker={{{,}}}
augroup END
" }}}
" Haml {{{
augroup ft_css
    au!
    au BufRead,BufNewFile *.hamlc set ft=haml
augroup END
" }}}
" HTML and handlebars {{{
augroup ft_html
    au!

    setlocal foldlevel=99
    au BufNewFile,BufRead *.html setlocal filetype=html
    au BufNewFile,BufRead *.hbs setlocal filetype=handlebars
    au FileType html,handlebars setlocal foldmethod=manual
    au FileType html,handlebars setlocal omnifunc=htmlcomplete#CompleteTags

    au FileType html,handlebars setlocal tabstop=4 shiftwidth=4 softtabstop=4

    " Use <localleader>f to fold the current tag.
    au FileType html,handlebars nnoremap <buffer> <localleader>f Vatzf

    " Indent tag
    au FileType html,handlebars nnoremap <buffer> <localleader>- Vat=

    " Underscore tags
    au FileType html,handlebars inoremap <buffer> <c-t> <%=<space><space>%><left><left><left>
    au FileType html,handlebars inoremap <buffer> <c-r> <%<space><space>%><left><left><left>

    " handlebars tags
    au FileType handlebars inoremap <buffer> <c-b> {{<space><space>}}<left><left><left>
augroup END
" }}}
" Java {{{
augroup ft_java
    au!

    au FileType java setlocal foldmethod=marker
    au FileType java setlocal foldmarker={,}

    au Filetype java setlocal makeprg=javac\ %
    au Filetype java setlocal errorformat=%A%f:%l:\ %m,%-Z%p^,%-C%.%#

    au Filetype java map <buffer> <F8> :make<Return>:copen<Return>
    au Filetype java map <buffer> <F9> :cprevious<Return>
    au Filetype java map <buffer> <F10> :cnext<Return>
augroup END
" }}}
" Javascript {{{
augroup ft_javascript
    au!

    au BufNewFile,BufRead jshintrc setlocal filetype=javascript
    au FileType javascript setlocal foldmethod=marker
    au FileType javascript setlocal foldmarker={,}
    au FileType javascript call MakeSpacelessBufferIabbrev('clog', 'console.log();<left><left>')
    au FileType javascript setlocal omnifunc=javascriptcomplete#CompleteJS
    au FileType javascript setlocal shiftwidth=4 softtabstop=4 tabstop=4
    au Filetype javascript setlocal foldlevel=99

    " Make {<cr> insert a pair of brackets in such a way that the cursor is correctly
    " positioned inside of them AND the following code doesn't get unfolded.
    au Filetype javascript inoremap <buffer> {<cr> {}<left><cr><space><space><space><space>.<cr><esc>kA<bs>
augroup END
" }}}
" JSON {{{
autocmd BufNewFile,BufRead *.json set ft=javascript

augroup json_autocmd
    autocmd!
    autocmd FileType json set formatoptions=tcq2l
    autocmd FileType json set shiftwidth=2 softtabstop=2 tabstop=8
    autocmd BufNewFile,BufRead *.json normal zR
augroup END
" }}}
" Markdown {{{
augroup ft_markdown
    au!

    au BufRead,BufNewFile *.{md,mdown,mkd,mkdn,markdown,mdwn} set filetype=markdown foldlevel=1
    au FileType markdown normal! zR
    au FileType markdown NeoBundleSource vim-markdown
    au FileType markdown setlocal omnifunc=htmlcomplete#CompleteTags

    " Use <localleader>1/2/3 to add headings.
    au Filetype markdown nnoremap <buffer> <localleader>1 yypVr=:redraw<cr>
    au Filetype markdown nnoremap <buffer> <localleader>2 yypVr-:redraw<cr>
    au Filetype markdown nnoremap <buffer> <localleader>3 mzI###<space>`zllll <ESC>
    au Filetype markdown set nofoldenable    " disable folding"
augroup END
" }}}
" Nginx {{{
augroup ft_nginx
    au!

    au BufRead,BufNewFile /etc/nginx/conf/*                      set ft=nginx
    au BufRead,BufNewFile /etc/nginx/sites-available/*           set ft=nginx
    au BufRead,BufNewFile /usr/local/etc/nginx/sites-available/* set ft=nginx
    au BufRead,BufNewFile vhost.nginx                            set ft=nginx

    au FileType nginx setlocal foldmethod=marker foldmarker={,}
augroup END
" }}}
" Postgresql {{{
augroup ft_postgres
    au!

    au BufNewFile,BufRead *.sql set filetype=pgsql
    au FileType pgsql set foldmethod=indent
    au FileType pgsql set softtabstop=2 shiftwidth=2
augroup END
" }}}
" Python {{{
augroup ft_python
    au!

    au FileType python setlocal define=^\s*\\(def\\\\|class\\)
    au FileType man nnoremap <buffer> <cr> :q<cr>
    au FileType python setlocal omnifunc=pythoncomplete#Complete
    au FileType python setlocal shiftwidth=4 softtabstop=4 tabstop=4

    " Jesus tapdancing Christ, built-in Python syntax, you couldn't let me
    " override this in a normal way, could you?
    au FileType python if exists("python_space_error_highlight") | unlet python_space_error_highlight | endif

    " Jesus, Python.  Five characters of punctuation for a damn string?
    au FileType python inoremap <buffer> <c-g> _(u'')<left><left>

    au FileType python inoremap <buffer> <c-b> """"""<left><left><left>
augroup END
" }}}
" QuickFix {{{
augroup ft_quickfix
    au!
    au Filetype qf setlocal colorcolumn=0 nolist nocursorline nowrap tw=0
augroup END
" }}}
" Ruby {{{
augroup ft_ruby
    au!
    au Filetype ruby setlocal foldmethod=syntax
    au BufRead,BufNewFile Capfile setlocal filetype=ruby
    au FileType ruby setlocal omnifunc=rubycomplete#Complete
augroup END
" }}}
" Scala {{{
augroup ft_scala
    au!
    au Filetype scala setlocal foldmethod=marker foldmarker={,}
    au Filetype scala compiler maven
    au Filetype scala let b:dispatch = 'mvn -B package install'
    au Filetype scala nnoremap <buffer> <localleader>s :SortScalaImports<cr>
    au Filetype scala nnoremap <buffer> M :call scaladoc#Search(expand("<cword>"))<cr>
    au Filetype scala vnoremap <buffer> M "ry:call scaladoc#Search(@r)<cr>
    au Filetype scala nmap <buffer> <localleader>( ysiwbi
    au Filetype scala nmap <buffer> <localleader>[ ysiwri
    ")]
augroup END
" }}}
" Vagrant {{{
augroup ft_vagrant
    au!
    au BufRead,BufNewFile Vagrantfile set ft=ruby
augroup END
" }}}
" Vim {{{
augroup ft_vim
    au!

    au FileType vim setlocal foldmethod=marker
    au FileType help setlocal textwidth=78
    au BufWinEnter *.txt if &ft == 'help' | wincmd L | endif
    au FileType vim setlocal shiftwidth=4 softtabstop=4 tabstop=4
augroup END
" }}}
" Vimrc {{{
" Autoload configuration when this file changes ($MYVIMRC)
augroup myvimrc
    au!
    au BufWritePost .vimrc,_vimrc,vimrc,.gvimrc,_gvimrc,gvimrc so $MYVIMRC | if has('gui_running') | so $MYVIMRC | endif
    augroup END
    " }}}
    " YAML {{{
    augroup ft_yaml
        au!

        au FileType yaml set shiftwidth=2
    augroup END
    " }}}
    " XML {{{
    augroup ft_xml
        au!

        au FileType xml setlocal foldmethod=manual
        au FileType xml setlocal omnifunc=xmlcomplete#CompleteTags

        " Use <localleader>f to fold the current tag.
        au FileType xml nnoremap <buffer> <localleader>f Vatzf

        " Indent tag
        au FileType xml nnoremap <buffer> <localleader>= Vat=
    augroup END
    " }}}
    " }}}
    " Plugin settings --------------------------------------------------------- {{{
    " Ag {{{
    nnoremap <leader>a :Ag<space>
    let g:ackprg = 'ag --smart-case --nogroup --nocolor --column'
    " }}}
    " Airline {{{
    let g:airline_powerline_fonts = 1
    let g:airline#extensions#syntastic#enabled = 1
    let g:airline#extensions#branch#enabled = 1
    " }}}
    " Commentary {{{
    nmap <leader>c <Plug>CommentaryLine
    xmap <leader>c <Plug>Commentary

    augroup plugin_commentary
        au!
        au FileType handlebars setlocal commentstring={{!\ %s\ }}
        au FileType htmldjango setlocal commentstring={#\ %s\ #}
        au FileType clojurescript setlocal commentstring=;\ %s
        au FileType puppet setlocal commentstring=#\ %s
        au FileType fish setlocal commentstring=#\ %s
    augroup END
    " }}}
    " Ctrl-P {{{
    let g:ctrlp_dont_split = 'NERD_tree_2'
    let g:ctrlp_jump_to_buffer = 0
    let g:ctrlp_working_path_mode = 0
    let g:ctrlp_match_window_reversed = 1
    let g:ctrlp_split_window = 0
    let g:ctrlp_max_height = 20
    let g:ctrlp_extensions = ['tag']
    let g:ctrlp_map = '<leader>,'
    let g:ctrlp_custom_ignore = 'static'

    let g:ctrlp_prompt_mappings = {
                \ 'PrtSelectMove("j")':   ['<c-j>', '<down>', '<s-tab>'],
                \ 'PrtSelectMove("k")':   ['<c-k>', '<up>', '<tab>'],
                \ 'PrtHistory(-1)':       ['<c-n>'],
                \ 'PrtHistory(1)':        ['<c-p>'],
                \ 'ToggleFocus()':        ['<c-tab>'],
                \ }

    let ctrlp_filter_greps = "".
                \ "egrep -iv '\\.(" .
                \ "jar|class|swp|swo|log|so|o|pyc|jpe?g|png|gif|mo|po" .
                \ ")$' | " .
                \ "egrep -v '^(\\./)?(" .
                \ ".git/|.hg/|.svn/" .
                \ ")'"

    let my_ctrlp_user_command = "" .
                \ "find %s '(' -type f -or -type l ')' -maxdepth 15 -not -path '*/\\.*/*' | " .
                \ ctrlp_filter_greps

    let my_ctrlp_git_command = "" .
                \ "cd %s && git ls-files --exclude-standard -co | " .
                \ ctrlp_filter_greps

    let my_ctrlp_ffind_command = "ffind --semi-restricted --dir %s --type e -B -f"

    " Use The Silver Searcher https://github.com/ggreer/the_silver_searcher
    if executable('ag')
        " Use Ag over Grep
        set grepprg="ag\ --nogroup\ --nocolor"

        " Use ag in CtrlP for listing files. Lightning fast and respects .gitignore
        let my_ctrlp_user_command = "" .
                    \ 'ag %s -l --nocolor -g "" | ' .
                    \ ctrlp_filter_greps

    endif

    let g:ctrlp_user_command = ['.git/', my_ctrlp_git_command, my_ctrlp_user_command]

    nnoremap <leader>. :CtrlPTag<cr>
    " }}}
    " Emmet {{{
    let g:user_emmet_leader_key = '<C-g>'
    let g:use_emmet_complete_tag = 1
    " }}}
    " Fugitive {{{
    nnoremap <leader>gd :Gdiff<cr>
    nnoremap <leader>gs :Gstatus<cr>
    nnoremap <leader>gw :Gwrite<cr>
    nnoremap <leader>ga :Gadd<cr>
    nnoremap <leader>gb :Gblame<cr>
    nnoremap <leader>gco :Gcheckout<cr>
    nnoremap <leader>gci :Gcommit<cr>
    nnoremap <leader>gm :Gmove<cr>
    nnoremap <leader>gr :Gremove<cr>
    nnoremap <leader>gl :Shell git gl -18<cr>:wincmd \|<cr>

    augroup ft_fugitive
        au!

        au BufNewFile,BufRead .git/index setlocal nolist
    augroup END

    " "Hub"
    nnoremap <leader>gh :Gbrowse<cr>
    vnoremap <leader>gh :Gbrowse<cr>
    " }}}
    " Gundo {{{
    nnoremap <F5> :GundoToggle<CR>

    let g:gundo_debug = 1
    let g:gundo_preview_bottom = 1
    let g:gundo_tree_statusline = "Gundo"
    let g:gundo_preview_statusline = "Gundo Preview"
    " }}}
    " HTML5 {{{
    let g:event_handler_attributes_complete = 0
    let g:rdfa_attributes_complete = 0
    let g:microdata_attributes_complete = 0
    let g:atia_attributes_complete = 0
    " }}}
    " Javascript {{{
    let g:javascript_conceal = 1
    " }}}
    " NERD Tree {{{
    noremap  <leader>n :NERDTreeToggle<cr>

    " Open the project tree and expose current file in the nerdtree with ,N
    nnoremap <Leader>N :NERDTreeFind<CR>

    augroup ps_nerdtree
        au!

        au Filetype nerdtree setlocal nolist
        au Filetype nerdtree nnoremap <buffer> H :vertical resize -10<cr>
        au Filetype nerdtree nnoremap <buffer> L :vertical resize +10<cr>
    augroup END

    let NERDTreeHighlightCursorline = 1
    let NERDTreeIgnore = ['\~$', '.*\.pyc$']

    let NERDTreeMinimalUI = 1
    let NERDTreeDirArrows = 1
    let NERDChristmasTree = 1
    let NERDTreeChDirMode = 2
    let NERDTreeMapJumpFirstChild = 'gK'
    " }}}
    " Syntastic {{{
    let g:syntastic_enable_signs = 1
    let g:syntastic_check_on_open = 1
    let g:syntastic_java_checker = 'javac'
    let g:syntastic_mode_map = {
                \ "mode": "active",
                \ "active_filetypes": [],
                \ "passive_filetypes": ['java', 'html', 'rst', 'scala']
                \ }

    let g:syntastic_disabled_filetypes = ['html', 'rst']
    let g:syntastic_stl_format = '[%E{%e Errors}%B{, }%W{%w Warnings}]'
    let g:syntastic_javascript_jshint_conf = '$HOME/.vim/jshintrc'

    let g:syntastic_error_symbol='✗'
    let g:syntastic_warning_symbol='⚠'
    let g:syntastic_style_error_symbol  = '⚡'
    let g:syntastic_style_warning_symbol  = '⚡'
    " }}}
    " Tabular {{{
    " let g:tabular_loaded = 1
    noremap <leader>T, :Tabularize /,<cr>
    noremap <leader>Tr :Tabularize /=><cr>
    noremap <leader>T= :Tabularize /=<cr>
    noremap <leader>T: :Tabularize /:<cr>
    noremap <leader>T<Bar> :Tabularize /<Bar><CR>
    " }}}
    " Tagbar {{{
    nmap <F3> :TagbarToggle<CR>
    " }}}
    " UltiSnips {{{
    let g:UltiSnipsExpandTrigger="<C-l>"
    let g:UltiSnipsJumpForwardTrigger="<C-l>"
    let g:UltiSnipsJumpBackwardTrigger="<C-h>"

    let g:UltiSnipsSnippetDirectories=["UltiSnips", "ultisnippets"]
    " }}}
    " Vim-Notes {{{
    let g:notes_directories = ['~/Documents/Notes', '~/Dropbox/Notes']

    " Make the C-] combination search for @tags:
    autocmd FileType notes imap <C-]> <C-o>:SearchNotes<CR>
    autocmd FileType notes nmap <C-]> :SearchNotes<CR>"
    " }}}
    " YankRing {{{
    nnoremap <silent> <F4> :YRShow<CR>

    function! YRRunAfterMaps()
        " Make Y yank to end of line.
        nnoremap Y :<C-U>YRYankCount 'y$'<CR>

        " Fix L and H in operator-pending mode, so yH and such works.
        omap <expr> L YRMapsExpression("", "$")
        omap <expr> H YRMapsExpression("", "^")

        " Don't clobber the yank register when pasting over text in visual mode.
        vnoremap p :<c-u>YRPaste 'p', 'v'<cr>gv:YRYankRange 'v'<cr>
    endfunction
    " }}}
    " YouCompleteMe {{{
    let g:ycm_key_list_select_completion = ['<TAB>', '<Down>', '<C-j>']
    let g:ycm_key_list_previous_completion = ['<S-TAB>', '<Up>', '<C-k>']
    " }}}
    " }}}
    " Mini-plugins ------------------------------------------------------------ {{{
    " Stuff that should probably be broken out into plugins, but hasn't proved to be
    " worth the time to do so just yet.

    " Synstack {{{
    " Show the stack of syntax hilighting classes affecting whatever is under the
    " cursor.
    function! SynStack()
        echo join(map(synstack(line('.'), col('.')), 'synIDattr(v:val, "name")'), " > ")
    endfunc

    nnoremap <F7> :call SynStack()<CR>
    " }}}
    " Indent Guides {{{
    let g:indentguides_state = 0
    function! IndentGuides() " {{{
        if g:indentguides_state
            let g:indentguides_state = 0
            2match None
        else
            let g:indentguides_state = 1
            execute '2match IndentGuides /\%(\_^\s*\)\@<=\%(\%'.(0*&sw+1).'v\|\%'.(1*&sw+1).'v\|\%'.(2*&sw+1).'v\|\%'.(3*&sw+1).'v\|\%'.(4*&sw+1).'v\|\%'.(5*&sw+1).'v\|\%'.(6*&sw+1).'v\|\%'.(7*&sw+1).'v\)\s/'
        endif
    endfunction " }}}

    hi def IndentGuides guibg=#303030
    nnoremap <leader>I :call IndentGuides()<cr>
    " }}}
    " Block Colors {{{
    let g:blockcolor_state = 0
    function! BlockColor() " {{{
        if g:blockcolor_state
            let g:blockcolor_state = 0
            call matchdelete(77881)
            call matchdelete(77882)
            call matchdelete(77883)
            call matchdelete(77884)
            call matchdelete(77885)
        else
            let g:blockcolor_state = 1
            call matchadd("BlockColor1", '^ \{4}.*', 1, 77881)
            call matchadd("BlockColor2", '^ \{8}.*', 2, 77882)
            call matchadd("BlockColor3", '^ \{12}.*', 3, 77883)
            call matchadd("BlockColor4", '^ \{16}.*', 4, 77884)
            call matchadd("BlockColor5", '^ \{20}.*', 5, 77885)
        endif
    endfunction " }}}
    " Default highlights {{{
    hi def BlockColor1 guibg=#222222
    hi def BlockColor2 guibg=#2a2a2a
    hi def BlockColor3 guibg=#353535
    hi def BlockColor4 guibg=#3d3d3d
    hi def BlockColor5 guibg=#444444
    " }}}
    nnoremap <leader>B :call BlockColor()<cr>
    " }}}
    " }}}
    " Environments (GUI/Console) ---------------------------------------------- {{{
    set t_Co=256

    if has('gui_running')
        " GUI Vim

        " set guifont=Menlo\ Regular\ for\ Powerline:h12
        set guifont=Meslo\ LG\ M\ Regular\ for\ Powerline:h12

        " Remove all the UI cruft
        set go-=T                       " Hide the toolbar
        set go-=m                       " Hide the menu
        set go-=l
        set go-=L
        set go-=r
        set go-=R

        highlight SpellBad term=underline gui=undercurl guisp=Orange

        " Different cursors for different modes.
        set guicursor=n-c:block-Cursor-blinkon0
        set guicursor+=v:block-vCursor-blinkon0
        set guicursor+=i-ci:ver20-iCursor

        map <silent> <F11>
                    \    :call system("wmctrl -ir " . v:windowid . " -b toggle,fullscreen")<CR>

        if has("gui_macvim")
            " Full screen means FULL screen
            set fuoptions=maxvert,maxhorz

            " Use the normal HIG movements, except for M-Up/Down
            let macvim_skip_cmd_opt_movement = 1
            no   <D-Left>       <Home>
            no!  <D-Left>       <Home>
            no   <M-Left>       <C-Left>
            no!  <M-Left>       <C-Left>

            no   <D-Right>      <End>
            no!  <D-Right>      <End>
            no   <M-Right>      <C-Right>
            no!  <M-Right>      <C-Right>

            no   <D-Up>         <C-Home>
            ino  <D-Up>         <C-Home>
            imap <M-Up>         <C-o>{

            no   <D-Down>       <C-End>
            ino  <D-Down>       <C-End>
            imap <M-Down>       <C-o>}

            imap <M-BS>         <C-w>
            inoremap <D-BS>     <esc>my0c`y

        elseif has("gui_gtk2")
            set guifont=Meslo\ LG\ M\ for\ Powerline\ 10

            " Maximize gvim window.
            set lines=999 columns=999
        else

            " Non-MacVim GUI
        end
    else

        " Console Vim
        " For me, this means iTerm2, possibly through tmux

        " Mouse support
        set mouse=a
    endif
    " }}}
    " TODO {{{
    " * Create a plugin to toggle between false and true with C-x and C-a.
    " * Add more customized snippets
    " }}}
